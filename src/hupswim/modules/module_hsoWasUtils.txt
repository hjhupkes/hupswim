Option Compare Database
Option Explicit

Public Function wasImpAppendZwmData(ByVal sZwmDBFName As String) As Boolean
On Error GoTo fout
wasImpAppendZwmData = False

DoCmd.DeleteObject acTable, "__was2005_zwemmers_inlees"

If Not (ImportDBase7Table(sZwmDBFName, "__was2005_zwemmers_inlees")) Then
  Exit Function
End If


Dim muContactID As Long
muContactID = Nz(DMax("CT_ID", "gIMP_LEN_CONTACT"), 0)



Dim sql As String
sql = "SELECT first(depotno) AS VER, startnr_from_was(nz([startno])) AS st, First(voorn) AS vn, " & _
      "First(achtern) AS an, First(tusvoegsel) AS vg, " & _
      "First([__was2005_zwemmers_inlees].mj) AS mj, " & _
      "First(gebdat) as gbd, first(telefoon) as tel, first(adres) as ad, " & _
      "First(huisno) as hsn, first(postcode) as pc, first(woonplaats) as pl, " & _
      "First(email) as em, first(depotno) as dep FROM __was2005_zwemmers_inlees GROUP BY startnr_from_was(nz([startno])) ORDER BY startnr_from_was(nz([startno])) ;"
      
Dim inv As Recordset
Set inv = CurrentDb().OpenRecordset(sql)

Dim rsZwms As Recordset
Set rsZwms = CurrentDb().OpenRecordset("select * from gIMP_LEN_Zwemmers where ZW_PartOfTimeArchive order by ZW_startnummer;")

inv.MoveFirst
rsZwms.MoveFirst

Dim rsCnt As Recordset
Set rsCnt = CurrentDb().OpenRecordset("gIMP_LEN_CONTACT")

Do Until inv.EOF Or rsZwms.EOF
  Dim blProcZwms As Boolean
  blProcZwms = False
  
  Dim tpStartZwms As String
  Dim tpStartInv As String
  
  tpStartZwms = Nz(rsZwms("zw_startnummer"))
  tpStartInv = Nz(inv("st"))
  
  
  If (tpStartZwms < tpStartInv) Then
    blProcZwms = True
  ElseIf (tpStartZwms > tpStartInv) Then
    blProcZwms = False
  Else
    'equality
    blProcZwms = True
    'add a contact id
    muContactID = muContactID + 1
    rsZwms.Edit
      rsZwms("ZW_Contact_ID") = muContactID
    rsZwms.Update
    
    rsCnt.AddNew
      rsCnt("CT_ID") = muContactID
      rsCnt("CT_Adres") = hzn(Trim(Nz(inv("ad")) & " " & Nz(inv("hsn"))))
      rsCnt("CT_Email") = hzn(Nz(inv("em")))
      rsCnt("CT_Plaats") = hzn(Nz(inv("pl")))
      rsCnt("CT_Postcode") = hzn(Nz(inv("pc")))
      rsCnt("CT_Telefoonnummer") = hzn(Nz(inv("tel")))
    rsCnt.Update
    
  End If
  
  
  
  If (blProcZwms) Then
    rsZwms.MoveNext
  Else
    inv.MoveNext
  End If
  
Loop

appCleanRS rsZwms
appCleanRS inv
appCleanRS rsCnt


DoCmd.DeleteObject acTable, "__was2005_zwemmers_inlees"

wasImpAppendZwmData = True
Exit Function

fout:
If (Err = 3021) Then Resume Next
If Err = 3011 Or Err = 7874 Then Resume Next ' could not find object to delete.
MsgBox Err & " wasImpAppendZwmData" & vbCrLf & Error$, 16
Exit Function
End Function