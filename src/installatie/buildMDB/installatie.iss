; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=Build hupswim/hupprog/packager from source
AppVerName=Build hupswim/hupprog from/packager source
AppPublisher=H.J. Hupkes - Hupsoft
AppPublisherURL=http://home.kpn.nl/wimhupke/hupsoft
AppSupportURL=http://home.kpn.nl/wimhupke/hupsoft
AppUpdatesURL=http://home.kpn.nl/wimhupke/hupsoft
DefaultDirName=c:\zwem
DefaultGroupName=Hupswim
Compression=lzma
SolidCompression=yes
AlwaysUsePersonalGroup = yes
UsePreviousTasks=no
AllowNoIcons=yes
CreateAppDir=no
Uninstallable=no
DisableProgramGroupPage=yes
DisableStartupPrompt=yes
DisableReadyMemo=yes
DisableReadyPage=no
DisableFinishedPage=yes

OutputDir=..\..\..
OutputBaseFileName=buildMDBs


[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Messages]
SetupWindowTitle=Build hupswim / hupprog / packager from source
WelcomeLabel1=Welcome to the build routines for hupswim / hupprog / packager
WelcomeLabel2=This will build hupswim / hupprog /packager from source.%n%nIt is recommended that you close all other applications before continuing.

WizardSelectTasks=Select Build options
SelectTasksDesc=Which applications do you want to build?
SelectTasksLabel2=Select the applications you would like to build, then click Next.


ButtonInstall=&Build

WizardReady=Ready to Build
ReadyLabel1=Ready to build selected applications.
ReadyLabel2b=Click Build to continue the build process.

WizardInstalling=Building
InstallingLabel=Please wait while the selected applications are being built.






[Tasks]
Name: buildHupswim; Description: "Build Hupswim"; GroupDescription: "Build options";  Check: not isHupswimFound();
Name: buildHupprog; Description: "Build Hupprog"; GroupDescription: "Build options";  Check: not isHupprogFound();
Name: buildPackager; Description: "Build Packager"; GroupDescription: "Build options";  Check: not isPackagerFound();

[Run]
;acc2003
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\11.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\hupsw97.mdb""  /x ""__stbSys_Restore_Tree"" /cmd ""{src}\src\hupswim""";  StatusMsg: "Building hupswim...";  Tasks: buildHupswim; Check: PrepareBuildHupswim() and UseAcc2003();
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\11.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\wedstr97.mdb"" /x ""__stbSys_Restore_Tree"" /cmd ""{src}\src\hupprog"""; StatusMsg: "Building hupprog...";  Tasks: buildHupprog; Check: PrepareBuildHupprog() and UseAcc2003();
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\11.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\packager.mdb"" /x ""__stbSys_Restore_Tree"" /cmd ""{src}\src\packager"""; StatusMsg: "Building packager...";  Tasks: buildPackager; Check: PrepareBuildPackager() and UseAcc2003();
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\11.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\hupsw97.mdb"" /compact  ";  StatusMsg: "Compacting hupswim..."; Tasks: buildHupswim; Check: IsHupswimBuilt() and UseAcc2003();
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\11.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\wedstr97.mdb"" /compact "; StatusMsg: "Compacting hupprog...";  Tasks: buildHupprog; Check: IsHupprogBuilt() and UseAcc2003();
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\11.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\packager.mdb"" /compact "; StatusMsg: "Compacting packager...";  Tasks: buildPackager; Check: IsPackagerBuilt() and UseAcc2003();

;acc2007
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\12.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\hupsw97.mdb""  /x ""__stbSys_Restore_Tree"" /cmd ""{src}\src\hupswim""";  StatusMsg: "Building hupswim...";  Tasks: buildHupswim; Check: PrepareBuildHupswim() and UseAcc2007();
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\12.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\wedstr97.mdb"" /x ""__stbSys_Restore_Tree"" /cmd ""{src}\src\hupprog"""; StatusMsg: "Building hupprog...";  Tasks: buildHupprog; Check: PrepareBuildHupprog() and UseAcc2007();
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\12.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\packager.mdb"" /x ""__stbSys_Restore_Tree"" /cmd ""{src}\src\packager"""; StatusMsg: "Building packager...";  Tasks: buildPackager; Check: PrepareBuildPackager() and UseAcc2007();
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\12.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\hupsw97.mdb"" /compact ";  StatusMsg: "Compacting hupswim..."; Tasks: buildHupswim; Check: IsHupswimBuilt() and UseAcc2007();
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\12.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\wedstr97.mdb"" /compact  "; StatusMsg: "Compacting hupprog...";  Tasks: buildHupprog; Check: IsHupprogBuilt() and UseAcc2007();
Filename: "{reg:HKLM\SOFTWARE\Microsoft\Office\12.0\Access\InstallRoot,Path}msaccess.exe"; Parameters: "/runtime ""{src}\packager.mdb"" /compact "; StatusMsg: "Compacting packager...";  Tasks: buildPackager; Check: IsPackagerBuilt() and UseAcc2007();



[Code]
var
  blHupswimFound:boolean;
  blHupprogFound:boolean;
  blPackagerFound:boolean;
  
  blFoundAcc2003:boolean;
  blFoundAcc2007:boolean;

function InitializeSetup(): Boolean;
begin
  blHupswimFound:=FileExists(expandConstant('{src}\hupsw97.mdb'));
  blHupprogFound:=FileExists(expandConstant('{src}\wedstr97.mdb'));
  blPackagerFound:=FileExists(expandConstant('{src}\packager.mdb'));

  result:=true;

  if (blHupswimFound and blHupprogFound and blPackagerFound) then begin
    msgbox('Build failed: hupswim [hupsw97.mdb], hupprog [wedstr97.mdb] and packager [packager.mdb] are already present in this directory.' , mbInformation, MB_OK);
    result:=false;
    exit;
  end;

  blFoundAcc2003:= RegKeyExists(HKEY_LOCAL_MACHINE, 'Software\microsoft\office\11.0\Access');
  blFoundAcc2007:= RegKeyExists(HKEY_LOCAL_MACHINE, 'Software\microsoft\office\12.0\Access');


  if (not (blFoundAcc2003 or blFoundAcc2007) ) then begin
    msgBox('Build failed: could not find installed version of Access 2003/2007.' , mbInformation, MB_OK);
    result:=false;
    exit;
  end;
  
  if not (dirExists(ExpandConstant('{src}\src\hupswim'))) then begin
    msgbox('Build failed: could not find required subdirectory: src\hupswim' , mbInformation, MB_OK);
    result:=false;
    exit;
  end;
  
  if not (dirExists(ExpandConstant('{src}\src\hupprog'))) then begin
    msgbox('Build failed: could not find required subdirectory: src\hupprog' , mbInformation, MB_OK);
    result:=false;
    exit;
  end;
  
  if not (dirExists(ExpandConstant('{src}\src\packager'))) then begin
    msgbox('Build failed: could not find required subdirectory: src\packager' , mbInformation, MB_OK);
    result:=false;
    exit;
  end;
  
  if not (FileExists(ExpandConstant('{src}\bin\empty_stub_acc2003.mdb'))) then begin
    msgbox('Build failed: could not find required file: bin\empty_stub_acc2003.mdb' , mbInformation, MB_OK);
    result:=false;
    exit;
  end;
  
end;





function IsHupswimFound():boolean;
begin
  result:=blHupswimFound;
end;

function IsHupprogFound():boolean;
begin
  result:=blHupprogFound;
end;

function IsPackagerFound():boolean;
begin
  result:=blPackagerFound;
end;




function IsHupswimBuilt():boolean;
begin
  result:=FileExists(expandConstant('{src}\hupsw97.mdb'));
end;

function IsHupprogBuilt():boolean;
begin
  result:=FileExists(expandConstant('{src}\wedstr97.mdb'));
end;

function IsPackagerBuilt():boolean;
begin
  result:=FileExists(expandConstant('{src}\packager.mdb'));
end;



function UseAcc2003(): Boolean;
begin
  result:=false;
  if (blFoundAcc2003) then begin
    result:=true;
  end;
end;

function UseAcc2007(): Boolean;
begin
  result:=false;
  if (blFoundAcc2007) and (not blFoundAcc2003) then begin
    result:=true;
  end;
end;

function PrepareBuildHupswim(): boolean;
begin
  result:=true;
  if not FileCopy(expandConstant('{src}\bin\empty_stub_acc2003.mdb'), expandConstant('{src}\hupsw97.mdb') , true ) then begin
    result:=false;
  end;
end;

function PrepareBuildHupprog(): boolean;
begin
  result:=true;
  if not FileCopy(expandConstant('{src}\bin\empty_stub_acc2003.mdb'), expandConstant('{src}\wedstr97.mdb') , true ) then begin
    result:=false;
  end;
end;

function PrepareBuildPackager(): boolean;
begin
  result:=true;
  if not FileCopy(expandConstant('{src}\bin\empty_stub_acc2003.mdb'), expandConstant('{src}\packager.mdb') , true ) then begin
    result:=false;
  end;
end;





